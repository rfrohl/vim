"" basics
set nocompatible
syntax enable
filetype plugin indent on

set number
set encoding=utf-8

" menu for choosing files
set wildmode=list:longest,full
set wildmenu
set wildignore=*.o,*.obj,*.aux,*.nav,*.out,*.snm,*.toc

" highlight the line with the cursor
set cursorline

" context around the cursor
set scrolloff=5

" write changes to file on certain occasions
set autowrite

set backspace=indent,eol,start

" make sure there is no sound when an error takes place
set noerrorbells
"set visualbell

" jump briefly to matching brackets
set noshowmatch " turned off: it is distracting with the current colorscheme
set matchtime=3

" formating
set textwidth=79
set colorcolumn=80

set listchars=eol:$,tab:>-,trail:~,extends:>,precedes:<

" Sets spaces used for (auto)indent
set tabstop=4
set shiftwidth=4
set softtabstop=4

" Tab key inserts spaces instead of tabs
set expandtab
set smarttab

" Indent to nearest tabstop
set shiftround

" indent
set cindent
set autoindent

" security: prevent modeline exploits
set modelines=0

"" search
set incsearch
set hlsearch
set ignorecase
set smartcase


"" replace
set gdefault    " :%s/foo/bar should replace in whole file not just current line


"" folding
set foldmethod=indent
set foldlevelstart=10


"" color scheme
colorscheme evening
set t_Co=256
set t_ut=
let g:rehash256=1
set background=dark


"" key mappings
let mapleader = ","

" :W should save as well
command W w
command Wq wq
command WQ wq
command Q q


" ========================================================================
" Function Keys - F*
" ========================================================================

" disable F1 help
nmap <F1> :echo<CR>
imap <F1> <C-o>:echo<CR>


"" Hexedit
nnoremap <F3> :Hexmode<CR>
inoremap <F3> <Esc>:Hexmode<CR>
vnoremap <F3> :<C-U>Hexmode<CR>

" ex command for toggling hex mode - define mapping if desired
command -bar Hexmode call ToggleHex()

" helper function to toggle hex mode
function ToggleHex()
    " hex mode should be considered a read-only operation
    " save values for modified and read-only for restoration later,
    " and clear the read-only flag for now
    let l:modified=&mod
    let l:oldreadonly=&readonly
    let &readonly=0
    let l:oldmodifiable=&modifiable
    let &modifiable=1
    if !exists("b:editHex") || !b:editHex
        " save old options
        let b:oldft=&ft
        let b:oldbin=&bin
        " set new options
        setlocal binary " make sure it overrides any textwidth, etc.
        let &ft="xxd"
        " set status
        let b:editHex=1
        " switch to hex editor
        %!xxd
    else
        " restore old options
        let &ft=b:oldft
        if !b:oldbin
            setlocal nobinary
        endif
        " set status
        let b:editHex=0
        " return to normal editing
        %!xxd -r
    endif
    " restore values for modified and read only state
    let &mod=l:modified
    let &readonly=l:oldreadonly
    let &modifiable=l:oldmodifiable
endfunction


" ========================================================================
" File type matching
" ========================================================================

if has("autocmd")
    " changes working directory to the directory of the last opened file
    au BufEnter * if expand("%:p:h") !~ '^/tmp' | lcd %:p:h | endif
    "autocmd BufEnter * :lcd %:p:h

    " go to the line where the last edit took place
    au BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$") | exe "normal g'\""| endif

    " Turn off line wrap for common files
    au BufNewFile,BufRead db.*	    setlocal nowrap nofen
    au BufNewFile,BufRead /etc/*	setlocal nowrap nofen

    "" C
    au FileType c         setl sw=8 ts=8 sts=8 noexpandtab cindent "list

    "" Ruby
    au FileType ruby      setl sw=2 sts=2 expandtab "list
endif

" ========================================================================
" Macros
" ========================================================================

"" trim trailing whitespaces
nnoremap <silent> <Leader>rts :call TrimWhiteSpace()<CR>

function! TrimWhiteSpace()
    %s/\s\+$//e
endfunction
